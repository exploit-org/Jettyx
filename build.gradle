plugins {
    id 'java'
    id 'java-library'
    id 'signing'
    id 'tech.yanand.maven-central-publish' version '1.2.0'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation project(":jettyx-http2")
    testImplementation project(":jettyx-jackson")

    testAnnotationProcessor('org.projectlombok:lombok:1.18.34')
    testCompileOnly 'org.projectlombok:lombok:1.18.34'

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    api 'org.eclipse.jetty:jetty-alpn-client:11.0.24'
    api 'org.eclipse.jetty:jetty-io:11.0.24'
    api 'org.eclipse.jetty:jetty-client:11.0.24'
    api 'com.bucket4j:bucket4j-core:8.10.1'
}

ext {
    projectName = "Jettyx"
    projectDescription = "A Retrofit-like HTTP Client wrapper for Jetty"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'
    apply plugin: 'tech.yanand.maven-central-publish'

    java {
        withSourcesJar()
        withJavadocJar()

        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    group = 'org.exploit'
    version = '0.1.5'

    afterEvaluate { p ->
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java

                    pom {
                        name = p.ext.projectName
                        description = p.ext.projectDescription
                        url = 'https://github.com/exploit-org/Jettyx'

                        licenses {
                            license {
                                name = 'BSD-2-Clause'
                                url = 'https://github.com/exploit-org/Jettyx/blob/main/LICENSE.md'
                            }
                        }

                        developers {
                            developer {
                                id = '0'
                                name = 'Martin Belov'
                                email = 'martin@exploit.org'
                            }
                        }

                        scm {
                            connection = 'scm:git:https://github.com/exploit-org/Jettyx.git'
                            developerConnection = 'scm:git:ssh://github.com/exploit-org/Jettyx.git'
                            url = 'https://github.com/exploit-org/Jettyx'
                        }
                    }
                }
            }

            repositories {
                maven {
                    name = "Local"
                    url = file("${buildDir}/repos/bundles")
                }
            }
        }

        signing {
            sign publishing.publications.mavenJava
        }

        mavenCentral {
            repoDir = layout.buildDirectory.dir('repos/bundles')

            authToken = System.getenv("CENTRAL_MAVEN_TOKEN")

            publishingType = 'USER_MANAGED'
        }
    }
}

jar {
    setDuplicatesStrategy(DuplicatesStrategy.EXCLUDE)

    from(configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }
}

test {
    useJUnitPlatform()
}